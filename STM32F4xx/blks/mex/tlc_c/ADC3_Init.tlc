%% $RCSfile: ADC3_Init.tlc,v $
%% $Revision: 1.0 $
%% $Date: 2012/01/26 $
%%
%% Abstract: ADC3 configuration
%%    STM32F4xx ADC3 configuration code generation.
%%    Generated code is based on STM32 Library files.
%%
%% Copyright 1990-2009 The MathWorks, Inc.

%implements ADC3_Init "C"

%if !EXISTS(::CreateADC3_Init)
    %assign ::CreateADC3_Init  = TLC_TRUE
%endif

%if !EXISTS(::InsertInitHeader)
    %assign ::InsertInitHeader  = TLC_TRUE
%endif
%if !EXISTS(::InsertIncludes)
    %assign ::InsertIncludes  = TLC_TRUE
%endif 

    %assign ::CreateADC3_Init  = TLC_TRUE

%trace CreateADC3_Init  %<CreateADC3_Init>


%% Create function for ADC3 configuration:
%if EXISTS(CreateADC3_Init) && (CreateADC3_Init == TLC_TRUE)

    %%assign ::CreateADC3_Init = TLC_FALSE

%% Code for system initialization:
    %function Start(block,system) Output
        %openfile ADC3_Buf
        %if EXISTS(::InsertInitHeader) && (::InsertInitHeader == TLC_TRUE)
            %assign ::InsertInitHeader    = TLC_FALSE
            /******************************************************
                            ADC3 INITIALIZATION
            *******************************************************/
        %endif
        /* ADC3 Initialization */
        ADC3_Init();
        %closefile ADC3_Buf
		%<LibMdlStartCustomCode(ADC3_Buf,"execution")>;
        %openfile ADC3_Buf
        %if EXISTS(::InsertInitHeader) && (::InsertInitHeader == TLC_TRUE)
            %assign ::InsertInitHeader    = TLC_FALSE
            /******************************************************
                            ADC3 START
            *******************************************************/
        %endif
        /* ADC3 START */
        ADC3_Start();
        %closefile ADC3_Buf
		%<LibMdlStartCustomCode(ADC3_Buf,"trailer")>;
    %endfunction

%% Code for ADC configuration call:
    %function BlockTypeSetup(block, system) void
        %assign SrcBaseName = LibGetMdlSrcBaseName()
        %assign modelH      = LibCreateSourceFile("Header", "Simulink", SrcBaseName)
        %assign modelC      = LibCreateSourceFile("Source", "Simulink", SrcBaseName)

		%assign ADC3_Num = SFcnParamSettings.ADC3_Num
		%assign ADC3_Description = SFcnParamSettings.ADC3_Description
		%assign ADC3_Selection = SFcnParamSettings.ADC3_Selection
		%assign ADC3_Default = SFcnParamSettings.ADC3_Default
		%assign ADC3_Guarded = SFcnParamSettings.ADC3_Guarded
		%assign ADC3_Offset = SFcnParamSettings.ADC3_Offset
		%assign ADC3_Resolution = SFcnParamSettings.ADC3_Resolution
		%assign ADC3_ScanMode = SFcnParamSettings.ADC3_ScanMode
		%assign ADC3_ConvMode = SFcnParamSettings.ADC3_ConvMode
		%assign ADC3_ExtTrigPol = SFcnParamSettings.ADC3_ExtTrigPol
		%assign ADC3_ExtTrigInjPol = SFcnParamSettings.ADC3_ExtTrigInjPol
		%assign ADC3_ExtTrig = SFcnParamSettings.ADC3_ExtTrig
		%assign ADC3_ExtTrigInj = SFcnParamSettings.ADC3_ExtTrigInj
		%assign ADC3_DataAlign = SFcnParamSettings.ADC3_DataAlign
		%assign ADC3_NbConv = SFcnParamSettings.ADC3_NbConv
		%assign ADC3_Pin = SFcnParamSettings.ADC3_Pin
		%assign ADC3_NbRegular = SFcnParamSettings.ADC3_NbRegular
		%assign ADC3_NbRegularGuarded = SFcnParamSettings.ADC3_NbRegularGuarded
		%assign ADC3_NbInjected = SFcnParamSettings.ADC3_NbInjected
		%assign ADC3_NbInjectedGuarded = SFcnParamSettings.ADC3_NbInjectedGuarded
		%assign ADC3_Watchdog_Enable = SFcnParamSettings.ADC3_Watchdog_Enable
		%assign ADC3_Watchdog_Command = SFcnParamSettings.ADC3_Watchdog_Command
		%assign ADC3_Watchdog_NumChan = SFcnParamSettings.ADC3_Watchdog_NumChan
		%assign ADC3_Watchdog_HighThreshold = SFcnParamSettings.ADC3_Watchdog_HighThreshold
		%assign ADC3_Watchdog_lowThreshold = SFcnParamSettings.ADC3_Watchdog_lowThreshold
        %openfile ADC3_Buf
       /*******************************************************************************
        * Function Name  : ADC3_Init
        * Description    : ADC3 Initialisation
        * Input          : -
        *******************************************************************************/		
        void ADC3_Init(void)
        {	
		%assign clockenable = 0
		%assign regularRank = 1
		%assign regularChan = 0
		%assign injectChan = 0
		%assign injectRank = 1
		%assign mode = "ADC_Mode_Independent"
		%assign Number = SIZE(ADC3_Description)
%warning "---------->Number of channels : %<Number[1]> "
		%assign numChan = 0
		%foreach Idx = %<Number[1]>
			%if ( ISEQUAL("%<ADC3_Description[Idx]>", "Regular") || ISEQUAL("%<ADC3_Description[Idx]>", "Injected") )
%warning "---------->Channel %<numChan> is : %<ADC3_Description[Idx]> "	
            /* Test if ADC3 clock has already been enabled*/
			%if (ISEQUAL("%<clockenable>", "0"))
			/* Enable ADC3 clock */			
			RCC_APB2PeriphClockCmd(RCC_APB2Periph_ADC3, ENABLE);
			/* ADC Common Init **********************************************************/
			ADC3_CommonInitStructure.ADC_Mode = %<mode>;
			ADC3_CommonInitStructure.ADC_Prescaler = ADC_Prescaler_Div2;
			ADC3_CommonInitStructure.ADC_DMAAccessMode = ADC_DMAAccessMode_Disabled;
			ADC3_CommonInitStructure.ADC_TwoSamplingDelay = ADC_TwoSamplingDelay_5Cycles;
			ADC_CommonInit(&ADC3_CommonInitStructure);
			%assign clockenable = 1
			%endif

			/* ADC3 channel %<numChan> Init ****************************************************************/
			/* Test Requested mode */
			%if (ISEQUAL("%<ADC3_Description[Idx]>", "Regular"))
			/*REGULAR MODE*/
			%assign regularChan = 1
			ADC3_InitStructure.ADC_Resolution = %<ADC3_Description[Idx+4]>;
			%if (ISEQUAL("%<ADC3_Description[Idx+5]>", "One_channel"))
			ADC3_InitStructure.ADC_ScanConvMode = DISABLE;
			%else
			ADC3_InitStructure.ADC_ScanConvMode = ENABLE;
			%endif
			%if (ISEQUAL("%<ADC3_Description[Idx+6]>", "on"))
			ADC3_InitStructure.ADC_ContinuousConvMode = ENABLE;
			%else
			ADC3_InitStructure.ADC_ContinuousConvMode = DISABLE;
			%endif
			ADC3_InitStructure.ADC_ExternalTrigConvEdge = %<ADC3_Description[Idx+7]>;
			ADC3_InitStructure.ADC_ExternalTrigConv = %<ADC3_Description[Idx+9]>;
			ADC3_InitStructure.ADC_DataAlign = %<ADC3_Description[Idx+11]>;
%%RPMODIF			ADC3_InitStructure.ADC_NbrOfConversion = %<ADC3_Description[Idx+12]>;
			ADC3_InitStructure.ADC_NbrOfConversion = %<ADC3_NbRegular>;
			ADC_Init(ADC3, &ADC3_InitStructure);	
			/* ADC3 regular channel%<numChan> configuration *************************************/
			ADC_RegularChannelConfig(ADC3, ADC_Channel_%<numChan>, %<regularRank>, ADC_SampleTime_3Cycles);
			%assign regularRank = %<regularRank> + 1
			%elseif (ISEQUAL("%<ADC3_Description[Idx]>", "Injected"))
			/*INJECTED MODE */
			%assign injectChan = 1
			ADC_SetInjectedOffset(ADC3, ADC_InjectedChannel_%<injectRank>,%<ADC3_Description[Idx+3]>)
			ADC3_InitStructure.ADC_Resolution = %<ADC3_Description[Idx+4]>;
			%if (ISEQUAL("%<ADC3_Description[Idx+5]>", "One_channel"))
			ADC3_InitStructure.ADC_ScanConvMode = DISABLE;
			%else
			ADC3_InitStructure.ADC_ScanConvMode = ENABLE;
			%endif
			%if (ISEQUAL("%<ADC3_Description[Idx+6]>", "on"))
			ADC3_InitStructure.ADC_ContinuousConvMode = ENABLE;
			%else
			ADC3_InitStructure.ADC_ContinuousConvMode = DISABLE;
			%endif
			ADC3_InitStructure.ADC_ExternalTrigConvEdge = %<ADC3_Description[Idx+8]>;
			ADC3_InitStructure.ADC_ExternalTrigConv = %<ADC3_Description[Idx+10]>;
			ADC3_InitStructure.ADC_DataAlign = %<ADC3_Description[Idx+11]>;
%%RPMODIF			ADC3_InitStructure.ADC_NbrOfConversion = %<ADC3_Description[Idx+12]>;
			ADC3_InitStructure.ADC_NbrOfConversion = %<ADC3_NbInjected>;
			ADC_Init(ADC3, &ADC3_InitStructure);	
			/* ADC3 injected channel%<numChan> configuration *************************************/
			ADC_InjectedChannelConfig(ADC3, ADC_Channel_%<numChan>, %<injectRank>, ADC_SampleTime_3Cycles);
			%assign injectRank = %<injectRank> + 1			
			%endif	
			%assign numChan = %<numChan> + 1
			%endif
			%if ( ISEQUAL("%<ADC3_Description[Idx]>", "Not_Used") )
			%assign numChan = %<numChan> + 1
			%endif
		%endforeach
			%if (ISEQUAL("%<ADC3_Watchdog_Enable>", "on"))
			/* Set watchdog threshold */
			ADC_AnalogWatchdogThresholdsConfig(ADC3, %<ADC3_Watchdog_HighThreshold>,%<ADC3_Watchdog_lowThreshold>);
			%if (ISEQUAL("%<ADC3_Watchdog_Command>", "ADC_AnalogWatchdog_SingleInjecEnable") || ISEQUAL("%<ADC3_Watchdog_Command>", "ADC_AnalogWatchdog_SingleRegOrInjecEnable"))			 
			/* Single channel watchdog configuration*/
			ADC_AnalogWatchdogSingleChannelConfig(ADC3, ADC_Channel_%<ADC3_Watchdog_NumChan>);
			%endif
			/* Watchdog enable */
			ADC_AnalogWatchdogCmd(ADC3, %<ADC3_Watchdog_Command>);
			%endif				
        }
        %closefile ADC3_Buf
        %<LibSetSourceFileSection(modelC,"Functions",ADC3_Buf)>
        %openfile ADC3_Buf
       /*******************************************************************************
        * Function Name  : ADC3_Start
        * Description    : Enable and start convertion for ADC3
        * Input          : -
        *******************************************************************************/					
        void ADC3_Start(void)
        {	
			%if (ISEQUAL("%<clockenable>", "1"))
			/* Enable ADC3 */
			ADC_Cmd(ADC3, ENABLE);
			%if (regularChan == 1)
			/* Start ADC3 Regular Software Conversion */ 
			ADC_SoftwareStartConv(ADC3);	
			%endif
			%if (injectChan == 1)
			/* Start ADC3 Injected Software Conversion */ 
			ADC_SoftwareStartInjectedConv(ADC3);
			%endif			
			%endif
		}
        %closefile ADC3_Buf
        %<LibSetSourceFileSection(modelC,"Functions",ADC3_Buf)>
		
		
%% Code for the function's declaration:
        %openfile ADC3_Buf
            /* Function Declaration for ADC3 Configuration */
            void ADC3_Init(void);
			void ADC3_Start(void);
        %closefile ADC3_Buf
        %<LibSetSourceFileSection(modelC,"Declarations",ADC3_Buf)>

%% Code for the necessary variable definition:
        %openfile ADC3_Buf
			/* Global Variable Definition for ADC3 Configuration */
			ADC_InitTypeDef       ADC3_InitStructure;
			ADC_CommonInitTypeDef ADC3_CommonInitStructure;
        %closefile ADC3_Buf
        %<LibSetSourceFileSection(modelC,"Definitions",ADC3_Buf)>

%% Code for required ADC3 includes:
        %if EXISTS(::InsertIncludes) && (::InsertIncludes == TLC_TRUE)
            %assign ::InsertIncludes    = TLC_FALSE
            %openfile ADC3_Buf
				#include "stm32f4xx.h"
            %closefile ADC3_Buf
            %<LibSetSourceFileSection(modelH,"Includes",ADC3_Buf)>
        %endif

    %endfunction
%else
%% ERROR ADC3 INIT MUST BE CALLED ONCE ONLY 
   %trace ERROR ADC3 INIT MUST BE CALLED ONCE ONLY 

%endif 

%% [EOF] ADC3_Init.tlc






